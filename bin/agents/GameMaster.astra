package agents;

agent GameMaster {
	module Cartago cartago;
	module Console console;
	module System system;
	
	types actions {
		formula playerAgentId(string);
		formula numberOfRounds(int);
		formula numberOfAgents(int);
	}
	
	rule +!main(list args) {
		cartago.startService();
		cartago.link();
		
		!setupTournamentArtifact(cartago.ArtifactId tournamentArtifact);
		!connectToTournamentArtifact();
		
		!setupStrategyArtifact(cartago.ArtifactId strategyArtifact);
		!connectToStrategyArtifact();
		
		!setupScoreBoardArtifact(cartago.ArtifactId scoreBoardArtifact);
		!connectToScoreBoardArtifact();
		
		cartago.configureTournament("configuration/config.json");
		cartago.populateStrategiesMap("configuration/config.json");
		cartago.createAgents();
		
		cartago.createNewRoundScoreRecorder(5, 5);
		cartago.newRound();
	}
	
	rule +!setupTournamentArtifact(cartago.ArtifactId tournamentArtifact) {
		cartago.makeArtifact("tournament", "tournament.core.TournamentArtifact", cartago.params([]), tournamentArtifact);
		console.println("Tournament artifact created");
    }
    
    rule +!connectToTournamentArtifact() {
		cartago.lookupArtifact("tournament", cartago.ArtifactId id);
		cartago.focus(id);
	}
    
    rule +!setupStrategyArtifact(cartago.ArtifactId strategiesArtifact) {
		cartago.makeArtifact("strategy", "strategies.core.StrategiesArtifact", cartago.params([]), strategiesArtifact);
		console.println("Strategy artifact created");
    }
    
    rule +!connectToStrategyArtifact() {
		cartago.lookupArtifact("strategy", cartago.ArtifactId id);
		cartago.focus(id);
	}
    
    rule +!setupScoreBoardArtifact(cartago.ArtifactId scoreBoardArtifact) {
		cartago.makeArtifact("score_board", "games.score.ScoreBoardArtifact", cartago.params([]), scoreBoardArtifact);
		console.println("Scoreboard artifact created");
    }
    
    rule +!connectToScoreBoardArtifact() {
		cartago.lookupArtifact("score_board", cartago.ArtifactId id);
		cartago.focus(id);
	}
    
    rule $cartago.signal(string id, createAgent(string agentId, string agentStrategy)) {
		system.createAgent(agentId, "agents.Player");
		system.setMainGoal(agentId, [agentStrategy]);
		console.println("created agent: " + agentId);
	}
	
    rule $cartago.signal(string id, startNewRound(int numberOfAgents, int num)) {
//    		console.println("New round started with " + numberOfAgents + " agents.");
		+numberOfAgents(numberOfAgents);
		+numberOfRounds(num);
		int i = 0;
		
		while (i < numberOfAgents) {
			string idString = i + "";
			send(request, idString, playerAgentId(idString));
			i = i + 1;
        }        
	}
	
	rule @message(request, string id, playerAgentId(string agentId)) {	
//		console.println("Agent with id " + agentId + " has received the request for a new round.");
		query(numberOfRounds(int num));
		query(numberOfAgents(int agentNum));
	
		cartago.storeCurrentRound(agentNum);
		cartago.createNewRoundScoreRecorder(agentNum, num);
		cartago.newRound();
	}
	
	rule $cartago.signal(string id, endTournament()) {
		console.println("Tournament is over.");
		cartago.printTournament();
	}
}
