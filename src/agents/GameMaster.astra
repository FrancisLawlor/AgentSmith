package agents;

agent GameMaster {
	module Cartago cartago;
	module Console console;
	module System system;
	
	types actions {
		formula playerAgentId(string);
		formula phaseSaver(string);
		formula numberOfRounds(int);
		formula numberOfAgents(int);
		formula restart(string);
	}
	
	rule +!main(list args) {
		cartago.link();
		
		!connectToTournamentArtifact();				
		!connectToScoreBoardArtifact();

		cartago.createAgents();
		
		cartago.getNumberOfAgents();
		cartago.getNumberOfRounds();
		
		query(numberOfRounds(int _numberOfRounds));
		query(numberOfAgents(int _numberOfAgents));
		cartago.createNewRoundScoreRecorder(_numberOfAgents, _numberOfRounds);
		cartago.newRound();
	}
    
    rule +!connectToTournamentArtifact() {
		cartago.lookupArtifact("tournament", cartago.ArtifactId id);
		cartago.focus(id);
	}
    
    rule +!connectToScoreBoardArtifact() {
		cartago.lookupArtifact("score_board", cartago.ArtifactId id);
		cartago.focus(id);
	}
    
    rule $cartago.signal(string id, createAgent(string agentId)) {
		system.createAgent(agentId, "agents.Player");
		system.setMainGoal(agentId, []);
		console.println("created agent: " + agentId);
	}
	
    rule $cartago.signal(string id, storeNumberOfAgents(int _numberOfAgents)) {
		+numberOfAgents(_numberOfAgents);
	}
	
	rule $cartago.signal(string id, storeNumberOfRounds(int _numberOfRounds)) {
		+numberOfRounds(_numberOfRounds);
	}
	
    rule $cartago.signal(string id, startNewRound()) {
		query(numberOfAgents(int _numberOfAgents));
		int i = 0;
		
		while (i < _numberOfAgents) {
			string idString = i + "";
			send(request, idString, playerAgentId(idString));
			i = i + 1;
        }        
	}
	
	rule @message(request, string id, playerAgentId(string agentId)) {	
		query(numberOfAgents(int _numberOfAgents));		
		cartago.storeCurrentRound(_numberOfAgents);
		send (request, "main", phaseSaver("game_master"));
	}
	
	rule $cartago.signal(string id, finishedStoringCurrentRound()) {	
		query(numberOfRounds(int _numberOfRounds));
		query(numberOfAgents(int _numberOfAgents));
		
		cartago.createNewRoundScoreRecorder(_numberOfAgents, _numberOfRounds);
		cartago.newRound();
	}
	
	rule $cartago.signal(string id, endTournament()) {
		console.println("Tournament is over.");
		send (request, "main", playerAgentId("game_master"));
	}
	
	rule $cartago.signal(string id, startNextNRounds()) {
		query(numberOfRounds(int _numberOfRounds));
		query(numberOfAgents(int _numberOfAgents));
		
		cartago.progressReport();
		cartago.createNewRoundScoreRecorder(_numberOfAgents, _numberOfRounds);
		cartago.newRound();
	}
}
