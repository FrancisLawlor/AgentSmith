package agents;

agent GameMaster {
	module Cartago cartago;
	module Console console;
	module System system;
	
	types actions {
		formula playerAgentId(string);
		formula numberOfRounds(int);
		formula numberOfAgents(int);
		formula restart(string);
	}
	
	rule +!main(list args) {
		console.println("bojack horseman");
		cartago.link();
		
		!connectToTournamentArtifact();				
		!connectToScoreBoardArtifact();

		cartago.createAgents();
		
		cartago.getNumberOfAgents();
		cartago.getNumberOfRounds();
		
		query(numberOfRounds(int _numberOfRounds));
		query(numberOfAgents(int _numberOfAgents));
		cartago.createNewRoundScoreRecorder(_numberOfAgents, _numberOfRounds);
		cartago.newRound();
	}
    
    rule +!connectToTournamentArtifact() {
		cartago.lookupArtifact("tournament", cartago.ArtifactId id);
		cartago.focus(id);
	}
    
    rule +!connectToScoreBoardArtifact() {
		cartago.lookupArtifact("score_board", cartago.ArtifactId id);
		cartago.focus(id);
	}
    
    rule $cartago.signal(string id, createAgent(string agentId)) {
		system.createAgent(agentId, "agents.Player");
		system.setMainGoal(agentId, []);
		console.println("created agent: " + agentId);
	}
	
    rule $cartago.signal(string id, storeNumberOfAgents(int _numberOfAgents)) {
		+numberOfAgents(_numberOfAgents);
	}
	
	rule $cartago.signal(string id, storeNumberOfRounds(int _numberOfRounds)) {
		+numberOfRounds(_numberOfRounds);
	}
	
    rule $cartago.signal(string id, startNewRound()) {
//    	console.println("New round started with " + numberOfAgents + " agents.");
		query(numberOfAgents(int _numberOfAgents));
		int i = 0;
		
		while (i < _numberOfAgents) {
			string idString = i + "";
			send(request, idString, playerAgentId(idString));
			i = i + 1;
        }        
	}
	
	rule @message(request, string id, playerAgentId(string agentId)) {	
//		console.println("Agent with id " + agentId + " has received the request for a new round.");
		query(numberOfAgents(int _numberOfAgents));		
		cartago.storeCurrentRound(_numberOfAgents);
	}
	
	rule $cartago.signal(string id, finishedStoringCurrentRound()) {	
		console.println("Finished storing current round.");
		query(numberOfRounds(int _numberOfRounds));
		query(numberOfAgents(int _numberOfAgents));
		
		cartago.createNewRoundScoreRecorder(_numberOfAgents, _numberOfRounds);
		cartago.newRound();
	}
	
	rule $cartago.signal(string id, endTournament()) {
		console.println("Tournament is over.");
		cartago.printTournament();
	}
	
	rule $cartago.signal(string id, startNextNRounds()) {
		query(numberOfRounds(int _numberOfRounds));
		query(numberOfAgents(int _numberOfAgents));
		
		cartago.createNewRoundScoreRecorder(_numberOfAgents, _numberOfRounds);
		cartago.newRound();
	}
}
